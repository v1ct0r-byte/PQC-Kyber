---Specification of CRYSTALS-KYBER till step 2
mod KYBERV2 is
    
    ---Sorts-------------------------------------------------------------------------------------------------------------
    
    sorts Identifier Principal Content Principals .
    sorts MsgState Msg Msgs .
    subsort Msg < Msgs . 
    subsort Principal < Principals .

    sorts Key Keys .
    subsort Key < Keys .
    subsort Key < Content .
    
    sort Ciphertext .
    subsort Ciphertext < Content .

    sort GlobalState .
    
    ---Operators-------------------------------------------------------------------------------------------------------------

    ---Empty group of keys
    op emptyK : -> Keys .
    ---Group of keys
    op _|_ : Keys Keys -> Keys [assoc id: emptyK comm] .

    ---Possible states for a message
    ops sent received consumed intercepted : -> MsgState .

    ---Empty content
    op emptyC : -> Content .
    ---Group of contents
    op __ : Content Content -> Content [assoc id: emptyC comm] .

    ---Structure of a message
    ---Sender, Receiver, State, Content
    op msg{(_,_)[_]_} : Identifier Identifier MsgState Content -> Msg .
    op emptyM : -> Msgs .
    op __ : Msgs Msgs -> Msgs [assoc id: emptyM] .

    ops Alice Eve Bob : ->  Identifier .
    ---Definition of the components of a Principal
    ---(idenifier that can be Alice, Bob or Eve [keys received or generated] content that the principal stores)
    op (_[_]_) : Identifier Keys Content -> Principal .

    ---Group of principals
    op __ : Principal Principal -> Principals [assoc comm] .

    ---Global state composed by Principals and a network with messages
    op <_>net(_) : Principals Msgs -> GlobalState .
    op True : -> GlobalState .

    ---Rules-------------------------------------------------------------------------------------------------------------
    
    vars P1 P2 : Principal .
    var PS : Principals .
    var MSGS : Msgs .
    ops pk sk k : -> Key .
    var ks1 ks2 : Keys .
    op Ciph : -> Ciphertext .
    var Cont Cont2 : Content .

    rl [KeyGen] : < (Alice[emptyK]emptyC) PS >net(emptyM) => < (Alice[pk | sk]emptyC) PS >net(emptyM) .

    rl [SendPK] : < (Alice[pk | ks1]Cont) PS >net(emptyM) => < (Alice[pk | ks1]Cont) PS >net(msg{(Alice,Bob)[sent]pk}) .

    rl [RecievePK] : < (Bob[ks1]Cont) PS >net(MSGS msg{(Alice,Bob)[sent]pk}) => < (Bob[pk | ks1]Cont) PS >net(MSGS msg{(Alice,Bob)[received]pk}) .
    
    rl [Enc] : < (Bob[pk | ks1]Cont) PS >net(MSGS msg{(Alice,Bob)[received]pk}) => < (Bob[k | pk | ks1]Ciph Cont) PS >net(MSGS msg{(Alice,Bob)[consumed]pk}).

    rl [SendCiph] : < (Bob[ks1]Ciph Cont) PS >net(MSGS msg{(Alice,Bob)[consumed]pk}) => < (Bob[ks1]Ciph Cont) PS >net(MSGS msg{(Alice,Bob)[consumed]pk} msg{(Bob,Alice)[sent]Ciph}) .

    rl [RecieveCiph] : < (Alice[ks1]Cont) PS >net(MSGS msg{(Bob,Alice)[sent]Ciph}) => < (Alice[ks1]Cont Ciph) PS >net(MSGS msg{(Bob,Alice)[received]Ciph}) .

    rl [Dec] : < (Alice[sk | ks1]Ciph Cont) PS >net(MSGS msg{(Bob,Alice)[received]Ciph}) => < (Alice[sk | k | ks1]Ciph Cont) PS >net(MSGS msg{(Bob,Alice)[consumed]Ciph}) .

    rl [Comp] : < (Alice[k | ks1]Cont) (Bob[k | ks2]Cont2) P1 >net(MSGS) => True .

    ---Initial state-------------------------------------------------------------------------------------------------------------
    
    op init : -> GlobalState .
    eq init = < (Alice[emptyK]emptyC) (Eve[emptyK]emptyC) (Bob[emptyK]emptyC) >net(emptyM) .
endm

---search in KYBERV2: init =>* True .