--- Author: Víctor García Valero <vicgarv2@upv.es>

mod KYBER-PREDS is
    protecting KYBER .
    including SATISFACTION .

    *** Defintion of GlobalState as subtype of State
    subsort GlobalState < State .

    *** Declaration of predicate to represent a principal starting the protocol
    op wantsToShareKey : Identifier Identifier -> Prop .
    
    *** Declaration of predicate to represent a principal finished the protocol
    op sharedAKeyWith : Identifier Identifier -> Prop .

    *** Declaration of predicate to represent a principal stole the shared key
    op stolenSharedKey : Identifier Identifier -> Prop .

    *** Variables for the equations
    vars ID1 ID2 : Identifier .
    var PS : Principals .
    var MSGS : Msgs .
    vars K1 SK : SKey .
    var PK : PKey .
    vars KS1 KS2 : Keys .
    vars CONT1 CONT2 CONT3 : Content .
    var GS : GlobalState .
    var P : Prop .

    *** Equations for the state's predicates

    *** Equations indicating two actors want to share a key
    eq {CONT3} < (ID1[publicKey(ID1,PK) ; KS1]CONT1) (ID2[KS2]CONT2) PS >net(MSGS msg{(ID1,ID2)[sentPK]PK}) |= wantsToShareKey(ID1,ID2) = true .

    *** Equations indicating two actors share a key
    eq {CONT3} < (ID1[sharedKey(ID2,K1) ; KS1]CONT1) (ID2[sharedKey(ID1,K1) ; KS2]CONT2) PS >net(MSGS) |= sharedAKeyWith(ID1,ID2) = true .

    *** Equations indicating an actor learned the secret key from another one
    eq {CONT3} < (ID1[secretKey(ID1,SK) ; KS1]CONT1) (ID2[secretKey(ID1,SK) ; KS2]CONT2) PS >net(MSGS) |= stolenSharedKey(ID1,ID2) = true .

    *** Equation to include any other cases as false
    eq GS |= P = false [owise] .
endm