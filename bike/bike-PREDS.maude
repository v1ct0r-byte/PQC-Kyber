--- Author: Víctor García Valero <vicgarv2@upv.es>

mod BIKE-PREDS is
    protecting BIKE .
    including SATISFACTION .

    *** Defintion of GlobalState as subtype of State
    subsort GlobalState < State .

    *** Declaration of predicate to represent a principal starting the protocol
    op wantsToShareKey : Identifier Identifier -> Prop .
    
    *** Declaration of predicate to represent a principal finished the protocol
    op sharedAKeyWith : Identifier Identifier -> Prop .

    *** Declaration of predicate to represent a principal stole a secret key
    op stolenSharedKey : Identifier Identifier -> Prop .

    *** Variables for the equations
    vars ID1 ID2 ID3 : Identifier .
    var PS : Principals .
    var MSGS : Msgs .
    var K : Key .
    vars KS1 KS2 KS3 : Keys .
    vars CONT1 CONT2 CONT3 CONT4 : Content .
    var GS : GlobalState .
    var P : Prop .

    *** Equations for the state's predicates

    *** Equation indicating two actors want to share a key
    eq {CONT3} < (ID1[publicKey(ID1,K) ; KS1]CONT1) (ID2[KS2]CONT2) PS >net(MSGS msg{(ID1,ID2)[sentPK]K}) |= wantsToShareKey(ID1,ID2) = true .

    *** Equation indicating two actors share a key
    eq {CONT3} < (ID1[sharedKey(ID2,K) ; KS1]CONT1) (ID2[sharedKey(ID1,K) ; KS2]CONT2) PS >net(MSGS) |= sharedAKeyWith(ID1,ID2) = true .

    *** Equation indicating an actor learned the shared key from another one (notice that ID3 does not need to have that shared key)
    eq {CONT4} < (ID1[sharedKey(ID3,K) ; KS1]CONT1) (ID2[sharedKey(ID1,K) ; KS2]CONT2) (ID3[KS3]CONT3) >net(MSGS) |= stolenSharedKey(ID1,ID2) = true .

    *** Equation to include any other cases as false
    eq GS |= P = false [owise] .
endm